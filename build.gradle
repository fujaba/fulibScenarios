plugins {
    id 'java'
    id 'antlr'
}

group 'org.fulib'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()

    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://mvnrepository.com/artifact/org.apache.bcel/bcel' }
}

// https://github.com/gradle/gradle/issues/820
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
    // https://mvnrepository.com/artifact/org.antlr/antlr4
    antlr group: 'org.antlr', name: 'antlr4', version: '4.7.2'

    // https://mvnrepository.com/artifact/org.antlr/antlr4-runtime
    compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.2'

    // https://mvnrepository.com/artifact/org.antlr/ST4
    compile group: 'org.antlr', name: 'ST4', version: '4.0.8'

    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'

    compile 'org.slf4j:slf4j-nop:1.7.25'

    compile 'org.fulib:fulib:1.0.+'
    compile 'org.fulib:fulibTools:1.0.+'
    compile 'org.fulib:fulibYaml:1.0.+'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// --------------- ANTLR ---------------

def generatedANTLRDir = "$buildDir/generated-src/antlr/main/java/"

sourceSets.main.java.srcDir(generatedANTLRDir)

// https://stackoverflow.com/questions/40995727/gradle-cant-find-antlr-token-file
generateGrammarSource {
    def packageName = 'org.fulib.scenarios.parser'
    arguments += [ '-package', packageName ]
    outputDirectory = file("$generatedANTLRDir/${ packageName.replace('.', '/') }/")
}

// --------------- GenTreeSrc ---------------

def gentreesrcDir = "$buildDir/generated-src/gentreesrc/main/"

configurations.create('gentreesrc')

dependencies {
    gentreesrc group: 'de.clashsoft', name: 'gentreesrc', version: '0.2.0'
}

tasks.register('gentreesrc', JavaExec) {

    classpath = configurations.gentreesrc
    main = 'de.clashsoft.gentreesrc.Main'
    args = [ 'src/main/gentreesrc/FulibScenarios.gts', gentreesrcDir ]

    inputs.dir("src/main/gentreesrc/")
    outputs.dir(gentreesrcDir)
}

sourceSets.main.java.srcDir(files(gentreesrcDir).builtBy('gentreesrc'))
