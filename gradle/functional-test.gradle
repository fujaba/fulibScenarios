// basis for plugin

def taskName = 'compileScenarios'
def sourceDirs = [ 'src/test/scenarios', 'webapp/examples/' ]
def modelOutputDir = "$buildDir/generated-src/scenarios/main/"
def testOutputDir = "$buildDir/generated-src/scenarios/test/"

tasks.register(taskName, JavaExec) {
    classpath = sourceSets.main.runtimeClasspath // change this in plugin
    main = 'org.fulib.scenarios.Main'

    args = [ '-m', modelOutputDir, '-t', testOutputDir ] + sourceDirs

    onlyIf { sourceDirs.any { file(it).exists()} }

    sourceDirs.each { inputs.dir(it) }
    outputs.dir(modelOutputDir)
    outputs.dir(testOutputDir)
}

// in plugin: compileJava.dependsOn taskName
compileTestJava.dependsOn taskName

sourceSets.test.java.srcDir(files(modelOutputDir).builtBy(taskName)) // in plugin: test -> main
sourceSets.test.java.srcDir(files(testOutputDir).builtBy(taskName))
