plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'antlr'
    id 'de.clashsoft.gentreesrc-gradle' version '0.3.0'
}

apply from: 'gradle/functional-test.gradle'

group 'org.fulib'
version '0.2.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()

    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://mvnrepository.com/artifact/org.apache.bcel/bcel' }
}

// https://github.com/gradle/gradle/issues/820
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
    gentreesrc group: 'de.clashsoft', name: 'gentreesrc', version: '0.4.0'

    // https://mvnrepository.com/artifact/org.antlr/antlr4
    antlr group: 'org.antlr', name: 'antlr4', version: '4.7.2'

    // https://mvnrepository.com/artifact/org.antlr/antlr4-runtime
    compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.2'

    // https://mvnrepository.com/artifact/org.antlr/ST4
    compile group: 'org.antlr', name: 'ST4', version: '4.0.8'

    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'

    compile 'org.slf4j:slf4j-nop:1.7.25'

    compile files('libs/fulib-1.0.2-SNAPSHOT.jar')
    compile 'org.fulib:fulib:1.0.+'
    compile 'org.fulib:fulibTools:1.0.+'
    compile 'org.fulib:fulibYaml:1.0.+'

    compile "com.sparkjava:spark-core:2.8.0"

    compile group: 'org.json', name: 'json', version: '20090211'

    // compile instead of testCompile because the web service uses it
    compile group: 'junit', name: 'junit', version: '4.12'
}

// --------------- ANTLR ---------------

def generatedANTLRDir = "$buildDir/generated-src/antlr/main/"

sourceSets.main.java.srcDir(generatedANTLRDir)

// https://stackoverflow.com/questions/40995727/gradle-cant-find-antlr-token-file
generateGrammarSource {
    def packageName = 'org.fulib.scenarios.parser'
    arguments += [ '-package', packageName ]
    outputDirectory = file("$generatedANTLRDir/${ packageName.replace('.', '/') }/")
}


jar {
    manifest {
        attributes 'Main-Class': 'org.fulib.scenarios.WebService'
    }
}
