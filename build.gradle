plugins {
    id 'java'
    id 'antlr'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'de.clashsoft.simple-publish' version '0.2.0'
    id 'de.clashsoft.gentreesrc-gradle' version '0.3.0'
}

apply from: 'gradle/functional-test.gradle'
apply from: 'gradle/webapp.gradle'

group 'org.fulib'
version '0.4.0'
description 'A compiler for textual example scenarios.'

publishInfo {
    websiteUrl = 'https://www.fulib.org/'
    issueTrackerUrl = 'https://github.com/fujaba/fulibScenarios'
    vcsUrl = 'https://github.com/fujaba/fulibScenarios'
    githubRepo = 'fujaba/fulibScenarios'
    labels = [ 'source', 'generator', 'scenario', 'markdown' ]

    license {
        shortName = 'BSD 3-Clause'
        longName = 'BSD 3-Clause "New" or "Revised" License'
        url = 'https://opensource.org/licenses/BSD-3-Clause'
    }

    developer {
        id = 'Clashsoft'
        name = 'Adrian Kunz'
        email = 'clashsoft@hotmail.com'
    }

    developer {
        id = 'azuendorf'
        name = 'Albert ZÃ¼ndorf'
        email = 'zuendorf@uni-kassel.de'
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

// https://github.com/gradle/gradle/issues/820
configurations {
    compile {
        extendsFrom = extendsFrom.findAll { it != configurations.antlr }
    }
}

dependencies {
    // --------------- Build Tools ---------------

    // https://mvnrepository.com/artifact/de.clashsoft/gentreesrc
    gentreesrc group: 'de.clashsoft', name: 'gentreesrc', version: '0.9.0'

    // https://mvnrepository.com/artifact/org.antlr/antlr4
    antlr group: 'org.antlr', name: 'antlr4', version: '4.7.2'

    // --------------- Fulib Libraries ---------------

    compile fileTree('libs/')
    // compile 'org.fulib:fulib:1.0.+'
    // compile 'org.fulib:fulibTools:1.0.+'
    // compile 'org.fulib:fulibYaml:1.0.+'

    // dependencies of fulibTools

    // https://mvnrepository.com/artifact/org.apache.commons/commons-text
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.4'

    // https://mvnrepository.com/artifact/guru.nidi/graphviz-java
    compile group: 'guru.nidi', name: 'graphviz-java', version: '0.8.8'

    // --------------- Other Libraries ---------------

    // https://mvnrepository.com/artifact/org.antlr/antlr4-runtime
    compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.7.2'

    // https://mvnrepository.com/artifact/org.antlr/ST4
    compile group: 'org.antlr', name: 'ST4', version: '4.0.8'

    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'

    compile 'org.slf4j:slf4j-nop:1.7.25'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// --------------- ANTLR ---------------

def generatedANTLRDir = "$buildDir/generated-src/antlr/main/"

sourceSets.main.java.srcDir(generatedANTLRDir)
sourceSets.main.java.srcDir("temp/shop/main")
sourceSets.test.java.srcDir("temp/shop/test")

// https://stackoverflow.com/questions/40995727/gradle-cant-find-antlr-token-file
generateGrammarSource {
    def packageName = 'org.fulib.scenarios.parser'
    arguments += [ '-package', packageName ]
    outputDirectory = file("$generatedANTLRDir/${ packageName.replace('.', '/') }/")
}

// --------------- Jars ---------------

jar {
    // include classes from jars in libs/
    from fileTree('libs/').files.collect { zipTree(it) }
}

// --------------- GenTreeSrc ---------------

gentreesrcJava.args '--visit-default'
